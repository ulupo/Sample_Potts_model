{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Graph Structure\n",
    "The coupling $J_{i,j}(\\sigma_i,\\sigma_j)$ will be take into account only if an edge exists between site $i$ and $j$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython\n",
    "%config IPCompleter.greedy=True \n",
    "import networkx as nx\n",
    "import numpy as np\n",
    "import matplotlib.pyplot as plt\n",
    "\n",
    "N = 200  # Number of nodes in the graph\n",
    "p = 0.02  # Probability that two nodes are connected \n",
    "graph = nx.erdos_renyi_graph(N, p)\n",
    "layout = nx.drawing.layout.fruchterman_reingold_layout(graph)  # For having the same layout at each drawing\n",
    "nx.draw(graph, with_labels=True, pos=layout)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Generation of data for variables belonging to more than two states\n",
    "In the case of the Potts model :\n",
    "$$\n",
    "P(\\sigma_1,.,\\sigma_N) = \\frac{\\exp\\left[\\frac{1}{T}\\left({\\sum_{i=1}^N  h_i(\\sigma_i) + \\sum_{i,j} J_{ij}(\\sigma_i,\\sigma_j)}\\right) \\right]}{ Z}\n",
    "$$\n",
    "What I need to generate data is:\n",
    "$$\n",
    "J_{ij}(\\sigma_i,\\sigma_j)  \\text{ and } h_i(\\sigma_i) \n",
    "$$\n",
    "The Metropolis--Hastings algorithm implies:\n",
    "$$\n",
    "P_{a\\rightarrow b} = \\min\\left\\{1, \\frac{\\pi(b)}{\\pi(a)}\\right\\} = \\min\\left\\{1,\\exp \\left[ \\frac{1}{T} \\left({{\\sum_{i=1}^N  h_i(\\sigma_i^b)- h_i(\\sigma_i^a) + \\sum_{i,j} J_{ij}(\\sigma_i^b,\\sigma_j^b) - J_{ij}(\\sigma_i^a,\\sigma_j^a)}}\\right)\\right] \\right\\}\n",
    "$$\n",
    "where $P_{a\\rightarrow b}$ is the transition probability in the MCMC algorithm and $\\pi(a)$ the stationary probability of being in the state $b$, i.e $P(\\sigma_1,.,\\sigma_N)$.\n",
    "\n",
    "IMPORTANT :\n",
    "Choice of Ising Gauge : \n",
    "$$\n",
    "\\sum_{\\sigma_i} J_{ij}(\\sigma_i,\\sigma_j) = \\sum_{\\sigma_j} J_{ij}(\\sigma_i,\\sigma_j) = 0\n",
    "$$\n",
    "To go in this Gauge, we infer $J_{ij}$ with $J_{ij} = C^{-1}_{ij}$ and after we apply :\n",
    "$$\n",
    "J^{\\mathrm{Ising}}_{ij}(\\sigma_i,\\sigma_j) = J_{ij}(\\sigma_i,\\sigma_j) - \\frac{1}{q}\\sum_{\\sigma_i} J_{ij}(\\sigma_i,\\sigma_j)  - \\frac{1}{q} \\sum_{\\sigma_j} J_{ij}(\\sigma_i,\\sigma_j) + \\frac{1}{q^2} \\sum_{\\sigma_i,\\sigma_j} J_{ij}(\\sigma_i,\\sigma_j) \n",
    "$$\n",
    "where q is the number of states for $\\sigma$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython --cplus --f\n",
    "import numpy as np\n",
    "import time\n",
    "cimport cython\n",
    "from libc.math cimport exp\n",
    "\n",
    "\n",
    "cdef extern from \"<random>\" namespace \"std\":\n",
    "    cdef cppclass mt19937:\n",
    "        mt19937() # we need to define this constructor to stack allocate classes in Cython\n",
    "        mt19937(unsigned int seed) # not worrying about matching the exact int type for seed\n",
    "    \n",
    "    cdef cppclass uniform_real_distribution[T]:\n",
    "        uniform_real_distribution()\n",
    "        uniform_real_distribution(T a, T b)\n",
    "        T operator()(mt19937 gen) # ignore the possibility of using other classes for \"gen\"\n",
    "\n",
    "    cdef cppclass uniform_int_distribution[T]:\n",
    "        uniform_int_distribution()\n",
    "        uniform_int_distribution(T a, T b)\n",
    "        T operator()(mt19937 gen) # ignore the possibility of using other classes for \"gen\"\n",
    "\n",
    "\n",
    "def test_uniform_distrs():\n",
    "    \"\"\"Small test for random number generation using https://www.cplusplus.com/reference/random/mt19937/\"\"\"\n",
    "    cdef:\n",
    "        mt19937 gen = mt19937(20)\n",
    "        uniform_real_distribution[double] dist_real = uniform_real_distribution[double](0.0, 1.0)\n",
    "        uniform_int_distribution[int] dist_int = uniform_int_distribution[int](0, 10)\n",
    "     \n",
    "    return dist_real(gen), dist_int(gen)\n",
    "\n",
    "\n",
    "cdef class PottsMSA:\n",
    "    cdef public double[:, ::1] field\n",
    "    cdef public double[:, :, :, ::1] coupling\n",
    "    cdef public int n_flips\n",
    "    cdef public float temperature\n",
    "    cdef public unsigned int seed\n",
    "    cdef int n_states\n",
    "    cdef int[:, :] nn_matrix\n",
    "    cdef int n_vars\n",
    "\n",
    "    def __init__(self, graph, double[:, ::1] field, double[:, :, :, ::1] coupling, float temperature, int n_flips):\n",
    "        \"\"\"\n",
    "        Input:\n",
    "        graph : networkx graph\n",
    "        double[:,::1] field : h_i(\\sigma_i)\n",
    "        double[:,:,:,::1] coupling : J_{i,j}(\\sigma_i,\\sigma_j)\n",
    "        float temperature \n",
    "        int n_flips : Number of flip to do \n",
    "        \"\"\"\n",
    "        self.n_states = np.intc(field.shape[1])\n",
    "        self.temperature = temperature\n",
    "        self.field = field\n",
    "        self.coupling = coupling\n",
    "        self.n_vars = np.intc(graph.number_of_nodes())\n",
    "        self.n_flips = n_flips\n",
    "        self._make_nn_matrix(graph)\n",
    "\n",
    "    def _make_nn_matrix(self, graph):\n",
    "        nn_matrix = -1 * np.ones((self.n_vars, self.n_vars), dtype=np.int32)\n",
    "        for node in range(self.n_vars):\n",
    "            neighbors = np.array([n for n in graph.neighbors(node)], dtype=np.int32)\n",
    "            if len(neighbors) > 0:\n",
    "                nn_matrix[node, :len(neighbors)] = neighbors\n",
    "        self.nn_matrix = nn_matrix\n",
    "\n",
    "    @cython.boundscheck(False)\n",
    "    @cython.wraparound(False)\n",
    "    @cython.initializedcheck(False)\n",
    "    def generate(self, int n_sequences, seed=None, init=None):\n",
    "        if init is None:\n",
    "            init = np.random.randint(self.n_states, size=(n_sequences, self.n_vars), dtype=np.int8)\n",
    "        if seed is None:\n",
    "            self.seed = time.time()\n",
    "        else:\n",
    "            self.seed = seed\n",
    "        cdef:\n",
    "            int i\n",
    "            char[:, ::1] init_view = init\n",
    "            int _seed = self.seed\n",
    "        self.MCMC(self.n_flips, init_view, _seed)\n",
    "\n",
    "        return init\n",
    "\n",
    "    @cython.boundscheck(False)\n",
    "    @cython.wraparound(False)\n",
    "    @cython.initializedcheck(False)\n",
    "    def hamiltonian(self, char[::1] sequence):\n",
    "        cdef:\n",
    "            int i, j\n",
    "            double h = 0\n",
    "        for i in range(self.nn_matrix.shape[0]):\n",
    "            h -= self.field[i, sequence[i]]\n",
    "            for j in range(self.nn_matrix.shape[1]):\n",
    "                if self.nn_matrix[i, j] == -1:\n",
    "                    break\n",
    "                elif self.nn_matrix[i, j] > i:  # Edge already included in Hamiltonian\n",
    "                    h -= self.coupling[i,\n",
    "                                       self.nn_matrix[i, j],\n",
    "                                       sequence[i],\n",
    "                                       sequence[self.nn_matrix[i, j]]]\n",
    "        return h \n",
    "\n",
    "    @cython.boundscheck(False)\n",
    "    @cython.wraparound(False)\n",
    "    @cython.initializedcheck(False)\n",
    "    cpdef void MCMC(self, int n_flips, char[:, ::1] init_view, int seed):  \n",
    "        cdef:\n",
    "            int selected_node, new_state\n",
    "            int c_mutation\n",
    "            double prob\n",
    "            mt19937 gen = mt19937(seed)\n",
    "            uniform_int_distribution[int] dist_states = uniform_int_distribution[int](0, self.n_states - 1)\n",
    "            uniform_int_distribution[int] dist_vars = uniform_int_distribution[int](0, self.n_vars - 1)\n",
    "            uniform_real_distribution[double] dist_prob = uniform_real_distribution[double](0.0, 1.0)\n",
    "\n",
    "        for i in range(init_view.shape[0]):\n",
    "            c_mutation = 0\n",
    "            sequence = init_view[i]\n",
    "            while c_mutation < n_flips:   \n",
    "                selected_node = dist_vars(gen)\n",
    "                new_state = dist_states(gen)\n",
    "                if new_state >= sequence[selected_node]:\n",
    "                    new_state += 1 \n",
    "                prob = exp((1 / self.temperature) * \n",
    "                           (self.pseudo_hamiltonian(selected_node, new_state, sequence) -\n",
    "                            self.pseudo_hamiltonian(selected_node, sequence[selected_node], sequence)))\n",
    "                if dist_prob(gen) < prob:\n",
    "                    sequence[selected_node] = new_state\n",
    "                    c_mutation += 1\n",
    "\n",
    "    @cython.boundscheck(False)\n",
    "    @cython.wraparound(False)\n",
    "    @cython.initializedcheck(False)\n",
    "    cdef double pseudo_hamiltonian(self, int node, int state_node, char[::1] sequence):\n",
    "        cdef:\n",
    "            int i\n",
    "            double h = 0\n",
    "        h = self.field[node, state_node]\n",
    "        for j in range(self.nn_matrix.shape[1]):\n",
    "                if self.nn_matrix[node, j] == -1:\n",
    "                    break\n",
    "                else:\n",
    "                    h += self.coupling[node,\n",
    "                                       self.nn_matrix[node, j],\n",
    "                                       state_node,\n",
    "                                       sequence[self.nn_matrix[node, j]]]\n",
    "        return h"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Generation of your MSA"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "n_sequences = 100\n",
    "# Set field, coupling, and temperature\n",
    "J = np.diagflat([2] * 20) - 1\n",
    "n_states = J.shape[0]\n",
    "n_vars = graph.number_of_nodes()\n",
    "\n",
    "coupling = np.array(np.broadcast_to(J, (n_vars, n_vars, n_states, n_states)),\n",
    "                    order='C', dtype=float) \n",
    "field = np.zeros((n_vars, n_states), dtype=float)\n",
    "\n",
    "temperature = 2\n",
    "\n",
    "# Number of flips accepted to do:\n",
    "n_flips = 4000  # You need to choose a good number of flips (You could look at the hamiltonian to choose)\n",
    "\n",
    "# Creation of the class:\n",
    "msa = PottsMSA(graph, field, coupling, temperature, n_flips)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "MSA = msa.generate(n_sequences)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Bonus plot the hamiltonian to see when equilibrium is reached"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import numpy as np \n",
    "\n",
    "Number_average = 1\n",
    "Delta_Flip = 100\n",
    "Max_Flip = 10**5\n",
    "LT = [0.5,1,2,3,4,5]\n",
    "\n",
    "Class_MSA_Generation = Creation_MSA(graph, field, coupling, temperature, Delta_Flip)\n",
    "\n",
    "L_energy = np.zeros((len(LT),Max_Flip//Delta_Flip))\n",
    "L_energy_var = np.zeros((len(LT),Max_Flip//Delta_Flip))\n",
    "\n",
    "for index_T, temperature in enumerate(LT):\n",
    "    MSA = np.random.randint(Number_of_state , size = (Number_of_sequence_in_MSA, graph.number_of_nodes()),dtype = np.int8)\n",
    "    print(\"T = \" + str(temperature))\n",
    "    Class_MSA_Generation.temperature = temperature\n",
    "    for index_flip in range(Max_Flip//Delta_Flip):\n",
    "        Halmitonian = [ Class_MSA_Generation.Hamiltonian(MSA[index_msa]) for index_msa in range(Number_of_sequence_in_MSA)]\n",
    "        L_energy[index_T,index_flip] = np.mean(Halmitonian)\n",
    "        Class_MSA_Generation.MSA_Equilibrium(1,MSA)\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L_flip = np.arange(Max_Flip//Delta_Flip)*Delta_Flip\n",
    "for index_T, temperature in enumerate(LT):\n",
    "    plt.plot(L_flip,L_energy[index_T],label = \"T = %s\"%temperature)\n",
    "plt.xlabel(\"Mutation accepted\")\n",
    "plt.ylabel(\"Hamiltonian\")\n",
    "plt.title(\"J = %s\" %J)\n",
    "plt.semilogx()\n",
    "plt.legend()\n",
    "plt.show()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
